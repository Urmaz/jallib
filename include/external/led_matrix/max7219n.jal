-- -----------------------------------------------------------------------------
-- Title: Library to support cascaded 8x8 led matrices with MAX7219
--
-- Author: Rob Hamerling, Copyright (c) 2014..2014, all rights reserved.
--
-- Adapted-by:
--
-- Compiler: 2.4q2
--
-- Revision: $Revision$
--
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Sources:
--
-- Description: Library for 'n' cascaded modules with MAX7219
-- - serial interface
-- - a number of 8 x 8 LED matrices
-- .
-- The max7219 module uses 3 data wires, which have to be specified by the user.
-- Before including this library declare the following aliases (mandatory):
-- .
--    alias max7219_din   is pin_??                    -- pin for data signal
--    alias max7219_clk   is pin_??                    -- pin for clock signal
--    alias max7219_cs    is pin_??                    -- pin for chip select signal
-- .
-- These pins must be configured for digital output by the user program.
-- .
-- The number of used cascaded must be specified as well, like:
-- .
--    const MAX7219_NUM = 2                           -- using 2 cascaded modules
-- .
-- This library supports a maximum of 8 cascaded modules
-- .
-- When you want to control LEDs individually while retaining the
-- state of the other LEDs a cache must be used. This is a builtin
-- feature of this library, but you have to indicate if you need it.
-- By default the cache is not active and some functions will not be
-- available. To activate the cache declare:
-- .
--    const bit MAX7219_DISPLAY_CACHE = TRUE
-- .
-- To initialize the chain of cascaded max7219 modules call procedure max7219_init()
--
-- Dependencies: max7219_common library
--
-- Notes: This library is exclusively designed to control a single 8x8 led matrix, the
--        features of the MAX7219 chip to display data on 7-segment displays are disabled.
--
-- -----------------------------------------------------------------------------

include max7219_common                 -- includes some basic sanity checks

-- --- additional sanity check --------------------

if !defined(MAX7219_NUM) then
   const byte MAX7219_NUM = 2                                -- default 1 module
   _warn "MAX7219_NUM was not defined, 2 cascaded modules assumed"
elsif (MAX7219_NUM == 1) then
   _warn "For a single (non cascaded) MAX7219 module better use library MAX7219"
elsif ((MAX7219_NUM < 2) | (MAX7219_NUM > 8)) then
   _error "MAX7219_NUM must be in the range 2..8"
end if

if !defined(MAX7219_DISPLAY_CACHE) then
   const bit MAX7219_DISPLAY_CACHE = FALSE               -- default no caching
end if
if (MAX7219_DISPLAY_CACHE == TRUE) then
   var byte max7219_cache[8 * MAX7219_NUM]               -- multi module cache
end if


-- ========= 'private' procedures =====================


-- ----------------------------------------------------------------------------
-- Title:  transfer addr + data to the max7219
-- Input:  - module number (0..7)
--         - address (command) byte
--         - data byte
-- Notes:  Data/command is for 1 module, others in the chain receive a NOP.
--         The last module in the chain is addressed first.
-- ----------------------------------------------------------------------------
procedure _max7219_write(byte in num, byte in addr, byte in data) is

   max7219_clk = LOW
   max7219_cs  = LOW
   for (MAX7219_NUM - num - 1) loop       -- trailing modules
      _max7219_shift_data(MAX7219_NO_OP, 0b_0000_0000)
   end loop
   _max7219_shift_data(addr, data)        -- the addressed module
   for num loop                           -- leading modules
      _max7219_shift_data(MAX7219_NO_OP, 0b_0000_0000)
   end loop
   max7219_cs = HIGH                      -- latch

end procedure


-- ----------------------------------------------------------------------------
-- Title:  shift a 16 bits sequence to all modules in the chain of MAX7219s
-- Input:  - addr
--         - data
-- Notes:  All modules receive the same addr + data
--         including (a single) latch
-- ----------------------------------------------------------------------------
procedure _max7219_write_all(byte in addr, byte in data) is

   max7219_clk = LOW
   max7219_cs  = LOW
   for MAX7219_NUM loop
      _max7219_shift_data(addr, data)
   end loop
   max7219_cs = HIGH                      -- latch

end procedure


-- ========== 'public' procedures ==================


-- ----------------------------------------------------------------------------
-- Title:  Clear display (all LEDs off)
-- Input:  (none)
-- Notes:  All LEDs of the chain will be cleared
--         (8 times the same column of all modules)
-- ----------------------------------------------------------------------------
procedure max7219_display_clear() is

   var byte i, j
   for 8 using i loop                     -- all 8 columns of a module
      if (MAX7219_DISPLAY_CACHE) then
         for MAX7219_NUM using j loop     -- column i in all modules
            max7219_cache[i + 8 * j] = 0b_0000_0000
         end loop
      end if
      _max7219_write_all(i + 1, 0b_0000_0000)
   end loop

end procedure


-- ----------------------------------------------------------------------------
-- Title:  Set LED intensity
-- Input:  LED intensity (range 0..15)
-- Notes:  All LEDs in the chain will have the same luminosity
-- ----------------------------------------------------------------------------
procedure max7219_display_intensity(byte in intensity) is

   _max7219_write_all(MAX7219_INTENSITY, intensity)

end procedure


-- ----------------------------------------------------------------------------
-- Title:  Initialize all modules with max7219
-- Input:  (none)
-- Notes:  Must be called before any other operation involving the display
-- ----------------------------------------------------------------------------
procedure max7219_init() is

   _max7219_write_all(MAX7219_SHUTDOWN,  0b_0000_0001)   -- return from shutdown
   _max7219_write_all(MAX7219_TEST,      0b_0000_0000)   -- return from test
   _max7219_write_all(MAX7219_DECODE,    0b_0000_0000)   -- no 7-segment decoding
   _max7219_write_all(MAX7219_SCANLIMIT, 0b_0000_0111)   -- all rows/columns active
   max7219_display_clear()                               -- all LEDs off
   max7219_display_intensity(MAX7219_DISPLAY_PWM3)       -- pretty low intensity

end procedure


-- ----------------------------------------------------------------------------
-- Title:  Put all MAX7219 modules in test mode for some time.
-- Input:  test period (byte, number of 0.1 seconds)
-- Notes:  All leds will be in maximum intensity during the specified period
-- ----------------------------------------------------------------------------
procedure max7219_display_test(byte in period) is

   _max7219_write_all(MAX7219_TEST, 0b_0000_0001)        -- test: all leds on
   delay_100ms(period)
   _max7219_write_all(MAX7219_TEST, 0b_0000_0000)        -- return from test

end procedure


-- ----------------------------------------------------------------------------
-- Title:  Blink whole module a number of times at a given speed
-- Input:  - number of times the display should blink off and on (byte)
--         - time in 0.1 seconds of the on and off periods (duty cycle is 50%)
-- Notes:  All 'on' LEDs of the whole chain will blink simultaneously.
-- ----------------------------------------------------------------------------
procedure max7219_display_blink(byte in times, byte in halfperiod) is

   for times loop
      _max7219_write_all(MAX7219_SHUTDOWN,  0b_0000_0000)  -- shutdown
      delay_100ms(halfperiod)
      _max7219_write_all(MAX7219_SHUTDOWN,  0b_0000_0001)  -- return from shutdown
      delay_100ms(halfperiod)
   end loop

end procedure


-- ----------------------------------------------------------------------------
-- Title: Display whole column (byte, 8 bits) of one module
-- Input: - module number (byte, 0..7)
--        - column number (byte, 0..7)
--        - byte with value to be displayed
-- ----------------------------------------------------------------------------
procedure max7219_display_byte_bin(byte in num, byte in col, byte in data) is

   col = col & 0x07                                      -- limit
   if (MAX7219_DISPLAY_CACHE) then
      max7219_cache[num * 8 + col] = data
   end if
   _max7219_write(num, col + 1, data)

end procedure


-- ----------------------------------------------------------------------------
-- Title: Display a column of leds (all leds in a column on or off)
-- Input: - module number (byte, 0..7)
--        - column number (byte, 0..7)
--        - led state (true or false, resp. on or off)
-- ----------------------------------------------------------------------------
procedure max7219_display_col(byte in num, byte in col, bit in state) is

   if (state == TRUE) then
      max7219_display_byte_bin(num, col, 0b_1111_1111)
   else
      max7219_display_byte_bin(num, col, 0b_0000_0000)
   end if

end procedure



-- === Following procedures are only available when cache is active ===

if (MAX7219_DISPLAY_CACHE) then

-- ----------------------------------------------------------------------------
-- Title: Set or reset a single LED
-- Input: - module number (byte, 0..7)
--        - column number (byte, 0..7)
--        - row number (byte, 0..7)
--        - led state (bit, true or false, on or off)
-- ----------------------------------------------------------------------------
procedure max7219_display_bit(byte in num, byte in col, byte in row, bit in state) is

   var byte mask

   num = num & 0x07
   col = col & 0x07
   mask = 0b_0000_0001 << (row & 0x07)
   if (state) then
      max7219_cache[num * 8 + col] = max7219_cache[num * 8 + col] | mask     -- set bit
   else
      max7219_cache[num * 8 + col] = max7219_cache[num * 8 + col] & (!mask)  -- clear bit
   end if
   _max7219_write(num, col + 1, max7219_cache[num * 8 + col])

end procedure


-- ----------------------------------------------------------------------------
-- Title: Display a row of leds (all leds in a row on or off) in one module
-- Input: - module number (byte, 0..7)
--        - row number (byte, 0..7)
--        - led state (bit, true or false, resp. on or off)
-- ----------------------------------------------------------------------------
procedure max7219_display_row(byte in num, byte in row, bit in state) is

   var byte i

   for 8 using i loop
      max7219_display_bit(num, i, row, state)
   end loop

end procedure


end if  -- max7219_display_cache present

