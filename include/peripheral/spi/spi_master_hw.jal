-- Title: SPI Master
-- Author: William Welch Copyright (c) 2009, all rights reserved.
-- Adapted-by: Sebastien Lelong
-- Sponsored by: Fiwihex www.fiwihex.com
-- Compiler: 2.4l
--
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: SPI Master hardware control.
-- Routines for sending and receiving through the SPI in master mode
--
-- Sources: good overview of SPI at http://en.wikipedia.org/wiki/Serial_Peripheral_Interface_Bus
-- also at: http://elm-chan.org/docs/spi_e.html
--

include spi_master_common

-- SPI is full-duplex, so we exchange master and slave data byte
function spi_master_hw_exchange(byte in m_data) return byte is

   SSPBUF = m_data

   if ( SSPCON_WCOL != 0 ) then
      -- FIXME how to report error???
      return 0xFF
   end if

   while ( SSPSTAT_BF == 0 ) loop end loop

   -- return slave data
   return SSPBUF
end function

-- half-duplex convenience function. send data to slave, discard reply
procedure spi_master_hw'put(byte in data) is
   var byte dummy
   dummy = spi_master_hw_exchange(data)
end procedure

-- half-duplex convenience function. send 0xFF, get slave data
function spi_master_hw'get() return byte is
   var byte data
   data = spi_master_hw_exchange(0xFF)
   return data
end function

procedure spi_master_hw_set_mode(byte in spi_mode) is
   if spi_mode == SPI_MODE_00 then
      SSPCON_CKP = 0
      SSPSTAT_CKE = 1
   elsif spi_mode == SPI_MODE_01 then
      SSPCON_CKP = 0
      SSPSTAT_CKE = 0
   elsif spi_mode == SPI_MODE_10 then
      SSPCON_CKP = 1
      SSPSTAT_CKE = 1
   else
      SSPCON_CKP = 1
      SSPSTAT_CKE = 0
   end if
end procedure

procedure spi_master_hw_set_speed(byte in spi_rate) is
   pragma inline
   SSPCON_SSPM = spi_rate
end procedure

-- Initialize the SPI registers.
-- Note: check your datasheet -- TRIS settings, any SPI errata?
procedure spi_init(byte in spi_mode, byte in spi_rate) is
   pragma inline
   SSPCON = 0
   SSPSTAT_SMP = 0

   spi_master_hw_set_mode(spi_mode)
   spi_master_hw_set_speed(spi_rate)

   SSPCON_SSPEN = 1
end procedure

