-- ----------------------------------------------------------------------
-- Title: math.jal - Collection of mathematical functions.
-- Author: Rob Hamerling, Copyright (c) 2009, all rights reserved.
-- Adapted-by:
-- Compiler: 2.4l
--
-- This file is part of jallib  (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description:
-- Collection of mathematical routines.
--
-- Sources: Several authors.
--
-- Notes:
--

-- --------------------------------------------------------
-- Calculate the square root of an unsigned 16-bits integer
-- Returns an 8-bits integer
-- Original author: Kyle York
-- --
function sqrt16(word in x) return byte is
   var word m, y

   m = 0x4000
   y = 0
   while (m != 0) loop
      var word b

      b = y | m
      y = y >> 1
      if (x >= b) then
         x = x - b
         y = y | m
      end if
      m = m >> 2
   end loop
   return byte(y)
end function


-- --------------------------------------------------------
-- Calculate the square root of an unsigned 32-bits integer
-- Returns a 16-bits integer
-- Original author: Kyle York
-- --
function sqrt32(dword in x) return word is
   var dword m, y

   m = 0x40000000
   y = 0
   while (m != 0) loop
      var dword b

      b = y | m
      y = y >> 1
      if (x >= b) then
         x = x - b
         y = y | m
      end if
      m = m >> 2
   end loop
   return word(y)
end function

