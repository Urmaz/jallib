-- Title: Network Initialization & Constants
-- Author: Matthew Schinkel - borntechi.com, copyright (c) 2009, all rights reserved.
-- Adapted-by:
-- Compiler: >=2.4l
--
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: Sets up the network link layer and defines constants
--
-- Sources:
-- http://www.comptechdoc.org/independent/networking/protocol/protnet.html
--

--------------------------------------------------------------------------------
-- Reads a recieve packet. Verifies IP Header data such as IP
-- address and checksum (with ip_header.jal), then sends the
-- rest of the data to the correct procedure depending on packet type.
--------------------------------------------------------------------------------
procedure network_received_packet(byte in packet_size) is
   ;if NETWORK_LINK_LAYER == SLIP then
   ;end if

   debug_crlf(DEBUG_2)
   const byte str1[] = "---------- PACKET RECEIVED ----------"
   debug_string(DEBUG_3, str1)
   debug_crlf(DEBUG_3)
   
   if packet_size > network_max_packet_size then
      -- your packet may have a problem, it is too large
      const byte str[] = "---------- ERROR: packet too large for buffer ----------"
      debug_string(debug_lvl_1, str)
      debug_crlf(debug_lvl_1)
      
      network_clear_rx_queue() -- clear bad data in the queues
      return                   -- ignore the packet
   end if
   
   -- check if the header destination ip matches PIC ip,
   -- and check if ip_header checksum is ok.
   if ip_header_verify() == 1 then
      debug_crlf(debug_lvl_3)

      -- check what type of packet it is (what protocol)
      if ip_header_protocol == ICMP then -- ICMP protocol
         const byte str2[] = "-- ICMP Packet --"
         debug_string(debug_lvl_3, str2)

         -- send the data to ICMP lib for reading
         icmp_read_packet(packet_size - 20)
      elsif ip_header_protocol == UDP then -- UDP protocol
         const byte str2[] = "-- UDP Packet --"
         debug_string(debug_lvl_3, str2)

         -- send the data to UDP lib for reading
         udp_read_packet(packet_size - 20)
      else                               -- unknown protocol
         const byte str3[] = "-- raw data --"
         debug_string(debug_lvl_3, str3)
         debug_crlf(debug_lvl_3)
         
         -- just print the raw data
         for packet_size - 20 loop               -- for each byte in packet
            debug_byte_hex(DEBUG_4, network_rx_buffer)
            debug_char(DEBUG_4," ")
         end loop
      end if
   else
      -- ignore the packet, it wasn't for us, or ip header checksum failed.
      -- must read all packet data to keep buffer in sync.
      debug_crlf(debug_lvl_3)
      const byte str2[] = "-- packet ignored --"
      debug_string(debug_lvl_3, str2)
      for packet_size - 20 loop
         -- ignore the packet
         var byte x = network_rx_buffer
      end loop
      debug_crlf(2)
   end if
   --
   debug_crlf(DEBUG_3)
end procedure

-- switch byte order in a word
procedure network_switch_word_bytes(word in out data) is
   var byte _data[2] at data
   var byte temp
   temp = _data[0]
   _data[0] = _data[1]
   _data[1] = temp
end procedure

-- switch byte order in a dword
procedure network_switch_dword_bytes(dword in out data) is
   var byte _data[4] at data
   var byte temp
   temp = _data[0]
   _data[0] = _data[3]
   _data[3] = temp

   temp = _data[1]
   _data[1] = _data[2]
   _data[2] = temp
end procedure





