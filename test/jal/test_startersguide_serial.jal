-- ------------------------------------------------------
-- Title: Test (Demo) program for startersguide (serial interface)
--
-- Author: Joep Suijs, Copyright (c) 2008..2010, all rights reserved.
--
-- Adapted-by: 
--
-- Compiler: >=2.4g
--
-- This file is part of jallib  (http://jallib.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description: Demo program, for use in the startersguide. Much of the code is not
--              meaningful on itself, but very usefull as part of the startersguide.
--
-- Sources:
--
-- Notes: as output, this sample produces characters on a serial link.
--
-- ------------------------------------------------------

;@jallib use chipdef
;@jallib use led

include delay

-- set all IO as digital
enable_digital_io()


-- setup serial (see echo.jal for more details)
;@jallib use serial
include serial_hardware
serial_hw_init()

-- use alias for output, so it is easy to modify
-- this sample for other devices like lcd
alias sg_output is serial_hw_data

include print

led_direction = output


const byte str_header[] = "\r\n------------------------------\r\n"

-- test header procedure
procedure test_header(byte in testnr) is
   const byte str1[] = "Test nr: "

   print_string(sg_output, str_header)
   print_string(sg_output, str1)
   print_byte_dec(sg_output, testnr)
   print_crlf(sg_output)
  
end procedure


-- pseudo variable part
var byte pv_store

procedure pv'put(byte in invar) is
   pv_store = invar + 1   
end procedure

function pv'get() return byte is
   return pv_store * 2
end function

-- definition of (dummy) procedure and fuctions
procedure sgd_initialize() is
   ;
end procedure

function sgd_transmit_byte(byte in data) return bit is  
	;
   data = 123
end function

function sgd_receive_function() return byte is      
   var byte data = 0
	;
   return data
end function

procedure sgd_receive_procedure(bit in ACK, byte in out data) is      
   data = 0
	;
end procedure


var  byte   alpha  = 0b0100_0011 -- binary 
;var  byte*4 bravo  = 0q203       -- octal 
var dword   charly = 0x43        -- hex
var  byte*3 delta  
var  byte   echo[3] at delta
var  byte   foxtrot[10]
var  byte   hotel[]   = { 1, 2, 4 }
var  byte   india[]    = "foxtrot"
var  bit    julia at alpha:0

const byte str1[] = "\r\n\r\nHello startersguide world!\r\n"

var byte x

forever loop
   delay_100ms(20)
   print_string(sg_output, str1)

   -- ----------------------------
   test_header(1) -- print header
   -- ----------------------------
   
   alpha = 1
;   if (Alpha > 0) then
;      Alpha = Alpha + 1
;   end if
; 
;   If (Alpha > 0) THEN
;      ALPHA = AlPhA + 1
;   END IF

   print_byte_dec(sg_output, alpha)

   -- ----------------------------
   test_header(2) -- print header
   -- ----------------------------

   charly = 0x1234
   alpha = charly

   alpha = byte(charly)
   
   print_byte_hex(sg_output, alpha)

   -- ----------------------------
   test_header(3) -- print header
   -- ----------------------------

   pv = 7
   print_byte_dec(sg_output, pv)
   sg_output = " "
   sg_output = "("
   print_byte_dec(sg_output, pv_store)
   sg_output = ")"

   -- ----------------------------
   test_header(4) -- print header      
   -- ----------------------------

   x = 2   
   if (x > 0) then
      x = x - 1
   end if

   if (x > 0) then
      x = x - 1
   else
      x = x + 1
   end if

   if (x == 1) then
      x = x + 1
   elsif (x > 7) then
      x = x + 2
   else
      x = x + 3
   end if
   print_byte_dec(sg_output, x)

   -- ----------------------------
   test_header(5) -- print header   
   -- ----------------------------

   x = 14   
   case (x) of
      1 : block
         x = 1
      end block
      2 : block
         x = 3
      end block
      3 : block
         x = 2
      end block
      otherwise block
         x = x / 2
      end block
   end case
   print_byte_dec(sg_output, x)

   -- ----------------------------
   test_header(6) -- print header    
   -- ----------------------------
   
   x = 0
   forever loop
      sg_output = "!"
      x = x + 1
      if (x > 10) then
         exit loop
      end if
   end loop

   -- ----------------------------
   test_header(7) -- print header    
   -- ----------------------------
   
   for 7 loop
      sg_output = "@"
   end loop 

   -- ----------------------------
   test_header(8) -- print header
   -- ----------------------------
   
   var byte lc
   for 7 using lc loop
      print_byte_dec(sg_output, lc)
      sg_output = " "
   end loop

   -- ----------------------------
   test_header(9) -- print header
   -- ----------------------------
   
   var byte x = 0
   repeat      
      sg_output = "#"
      x = x + 1
   until (x == 0)

   -- ----------------------------
   test_header(10) -- print header
   -- ----------------------------
   
   x = 0
   while (x > 0) loop 
      sg_output = "$"
      x = x - 1
   end loop

   -- ----------------------------
   test_header(11) -- print header
   -- ----------------------------
   
   x = 4
   while (x > 0) loop 
      sg_output = "%"
      x = x - 1
   end loop

   -- ----------------------------   
   -- closing test output
   print_string(sg_output, str_header)
   print_crlf(sg_output)   
   -- ----------------------------
end loop

; Output:      
;
; Hello startersguide world!
; 
; ------------------------------
; Test nr: 1
; 1
; ------------------------------
; Test nr: 2
; 34
; ------------------------------
; Test nr: 3
; 16 (8)
; ------------------------------
; Test nr: 4
; 3
; ------------------------------
; Test nr: 5
; 7
; ------------------------------
; Test nr: 6
; !!!!!!!!!!!
; ------------------------------
; Test nr: 7
; @@@@@@@
; ------------------------------
; Test nr: 8
; 0 1 2 3 4 5 6
; ------------------------------
; Test nr: 9
; ################################################################################
; ################################################################################
; ################################################################################
; ################
; ------------------------------
; Test nr: 10
; 
; ------------------------------
; Test nr: 11
; %%%%
; ------------------------------