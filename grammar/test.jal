
;include pic_target
include pc_target



;------------------------------------------------------------------------------
; test of forever loop, if elsif, else, exit loop
test_header(1)

var word e
 e = 5

function plus_five (byte in param) return byte is
   return param + 5
end function

forever loop   

   if (e == 10) then 
      e=20 
   elsif e==30 then 
      e=40 
   elsif e==50 then 
      exit loop
   else 
      e=plus_five(e) 
   end if

   print_var(e)

end loop

;------------------------------------------------------------------------------
; while loop test
test_header(2)

e = 0
while (e < 35) loop  

   if (e == 10) then 
      e=20 
   elsif e==30 then 
      e=40 
   elsif e==60 then 
      exit loop
   else 
      e=plus_five(e) 
   end if

   print_var(e)

end loop

;------------------------------------------------------------------------------
; repeat until   
test_header(3)

repeat

   if (e == 10) then 
      e=20 
   elsif e==30 then 
      e=40 
   elsif e==60 then 
      exit loop
   else 
      e=plus_five(e) 
   end if

   print_var(e)

until(e < 35) 


;------------------------------------------------------------------------------
;  for loop
test_header(4)

e = 5
for 4 loop

   e = e + 1
   print_var(e)

end loop 

for 4 using e loop

   print_var(e)

end loop 

;------------------------------------------------------------------------------
; case and block             
test_header(5)

for 10 using e loop
   case e of
      0 : print_var(0)
      1, 2 : print_var(12)
      
      3, 4 : block 
         print_var(34)
      end block
      
      5, 6 : block
      end block
   
      otherwise print_var(100+e)
   end case
end loop              

;------------------------------------------------------------------------------
; test call by reference
test_header(5)

procedure increment(byte in out v) is
   v = v + 1
end procedure

var byte a = 1

increment(a)

print_var(a)

;------------------------------------------------------------------------------
; test call by reference II
test_header(6)

procedure intermediate(byte in out w) is
   increment(w)
end procedure

a = 1

intermediate(a)

print_var(a)

;------------------------------------------------------------------------------
; pvar put and get
test_header(7)

procedure aap'put(byte in p) is
   print_var(100 + p)
end procedure

function  aap'get() return byte is
   return 7
end function

aap = aap 

;------------------------------------------------------------------------------
;------------------------------------------------------------------------------
; test group:
;  call *by* code, reference and value X call *from* code, reference, value
;------------------------------------------------------------------------------
;------------------------------------------------------------------------------

procedure callby_code(volatile byte in out v) is
   v = v + 1
end procedure

procedure callby_reference(byte in out v) is
   v = v + 2
end procedure

var byte a

;------------------------------------------------------------------------------
; test call by code, reference and value from *value*
test_header(9)

procedure TestFromValue(byte in c) is
   callby_code(c)
   callby_reference(c)
   print_var(c)
end procedure

a = 1
TestFromValue(a)
print_var(a)

;------------------------------------------------------------------------------
; test call by code, reference and value from *reference*
test_header(10)                                          

procedure TestFromReference(byte in out b) is
   callby_code(b)
   callby_reference(b)
   print_var(b)
end procedure

a = 1
TestFromReference(a)
print_var(a)

;------------------------------------------------------------------------------
; test call by code, reference and value from *code*
test_header(11)

procedure TestFromCode(volatile byte in out b) is
   callby_code(b)
;   callby_reference(b)
   print_var(b)
end procedure

a = 1
TestFromCode(a)
print_var(a)

