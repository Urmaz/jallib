-- Title: read TC77 temperatures, show on LCD
-- Author: Eur van Andel, eur@fiwihex.nl Copyright (c) 2008
-- Adapted-by:
-- Compiler: =2.4h
-- 
-- This file is part of jallib (http://jallib.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description: this sample shows how to read the temperature in Celsius
-- with different precisions from the TC77 sensor.
-- It demonstrates the different routines in the TC77 library.
--

-- select PIC
include 16f876a

pragma target clock      20_000_000  -- 20 MHz xtal
pragma target OSC       HS
pragma target WDT       disabled    -- no watchdog, please
pragma target LVP       disabled    -- no low voltage programming

enable_digital_io()                 -- no analog pins here


-- ------------------- INCLUDE LIBRARIES --------------------------

var  bit  LCD_RS           is pin_b5      -- LCD command/data select.
var  bit  LCD_EN           is pin_b4      -- LCD data trigger
var byte  lcd_data_port    is portb_low   -- LCD data nibble
portb_direction            = all_output   -- LCD data is portb_low
include lcd_hd44780_4                     -- LCD library with 4 data lines
include print                             -- nice formatted output 

var bit CS                 is pin_a0      -- chip select of TC77, active low
CS                         = high
pin_a0_direction           = output  
var bit SIO                is pin_a2      -- data in/out of TC77
pin_a2_direction           = input        -- only used as input here
var bit SCK                is pin_a1      -- clock of TC77
pin_a1_direction           = output  
include tc77                              -- TC77 library

-- ------------------- VARIABLES --------------------------------------

var word    temperature_raw             -- raw 13-bit value, with sign
var sbyte   temperature_8               -- signed Celsius, precision 1C
var sword   temperature_16              -- signed Celsius, precision 0.01C
var sdword  temperature_32              -- signed Celsius, precision 0.0001C

-- --------------- START PROGRAM ---------------------------

lcd_clearscreen()

forever loop
   lcd_set_cursor(0,0) 
   CS = low
   read_raw_tc77(temperature_raw)
   CS = high
   print_word_binary(lcd, temperature_raw)
   
   lcd_set_cursor(1,0) 
   CS = low
   read_celsius_sbyte_tc77(temperature_8)
   CS = high
   print_sbyte_dec(lcd, temperature_8)
      
   lcd_set_cursor(0, 20)                   -- row 3, position = 1 
   CS = low
   read_celsius_sword_tc77(temperature_16)
   CS = high
   _print_suniversal_dec(lcd, temperature_16, 10000, 2)

   lcd_set_cursor(1, 20)                   -- row 4, position = 1 
   CS = low
   read_celsius_sdword_tc77(temperature_32)
   CS = high
   _print_suniversal_dec(lcd, temperature_32, 100000000, 5)
   
   delay_100ms(2)                         -- for proper display viewing
   
end loop
